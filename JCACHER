/* REXX */ 
JRC = 0 
parse arg sfw cmd_in 
UPPER="ABCDEFGHIJKLMNOPQRSTUVWXYZ" 
lower="abcdefghijklmnopqrstuvwxyz" 
sfw = TRANSLATE(sfw,LOWER,UPPER) 
if sfw = 'all' then 
do 
  sfwc.1 = 'zcn' 
  sfwc.2 = 'rse' 
  sfwc.3 = 'eqa' 
  sfwc.4 = 'zosmf' 
  sfwc.0 = 4 
end 
else 
do 
  sfwc.1 = sfw 
  sfwc.0 = 1 
end 
do main = 1 to sfwc.0 
  sfw = sfwc.main 
  say "Attempting "cmd_in" for: "sfw 
  out.0 = 0 
  err.0 = 0 
  env.1 = "/usr/lpp/java/J8.0_64/bin/" 
  env.0 = 1 
  in.0 = 0 
  jc="java -Xshareclasses:cacheDir=/javasc/" 
  jc=jc||sfw",name="sfw"cache,"cmd_in 
  call bpxwunix jc,in.,out.,err.,env.,1,0 
  do lp = 1 to out.0 
    say "OUTPUT: "out.lp 
  end 
  do lp = 1 to err.0 
    parse var err.lp msgId . 
    select 
      when cmd_in = 'snapShotcache' then 
      do 
        JRC = 8 
        if msgId = 'JVMSHRC700I' then 
        do 
          JRC = 0 
          say "Successfully created Snapshot" 
          if sfw = 'zcn' then uid = 'ZOSCSRV:ZOSCGRP' 
          if sfw = 'eqa' then uid = 'STCEQA:SYS1' 
          if sfw = 'rse' then uid = 'STCRSE:STCGROUP' 
          if sfw = 'zosmf' then uid = 'IZUSVR:IZUADMIN' 
          cmd = "chown "uid" /javasc/"sfw"/javasharedresources/*"
          call bpxwunix cmd,,outc. 
          leave lp 
        end 
      end 
      when cmd_in = 'destroySnapshot' then 
      do 
        JRC = 9 
        if msgId = 'JVMSHRC718E' then 
        do 
          JRC = 0 
          say "Snapshot not found .. returning as success" 
          leave lp 
        end 
        if msgId = 'JVMSHRC810I' then 
        do 
          JRC = 0 
          say "Successfully destroyed Snapshot" 
          leave lp 
        end 
      end 
      otherwise 
      do 
        say err.lp 
        JRC = 0 
      end 
    end 
  end 
                                                            
  If JRC = 8 then say "ERROR Creating Snap Shot for "sfw 
  If JRC = 9 then say "ERROR Destroying Snap Shot for "sfw 
end 
return JRC 
